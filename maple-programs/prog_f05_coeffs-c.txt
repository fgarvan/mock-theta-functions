  T:=1000000:
  comp_f05:=proc(T)
    local f05A,aA,k,n,n1,j,k1,k2,f05AMAT,f05Aq;
    ########################################################################
    # INITIALIZE Arrays f05A and aA
    f05A:=Array(0..T):
    aA:=Array(0..T):
    for k from 0 to T do aA[k]:=0: f05A[k]:=0: od:
    f05A[0]:=1: 
    ########################################################################
    # We compute the coefficients a(n):
    for n from 0 to trunc( (sqrt(1+24*T)-1)/6) do
       n1:=n*(5*n+1)/2: 
       for j from -n to n  do
          k:=n1-j^2: if k<= T then
          aA[k]:= aA[k]+ (-1)^(j): fi:
        od:
    od:
    for n from 0 to trunc( (sqrt(33+24*T)-9)/6) do
       n1:=n*(5*n+1)/2+4*n+2:
       for j from -n to n do 
          k:=n1-j^2: if k<=T then
          aA[k]:= aA[k]+ (-1)^(j+1): fi:
        od:
    od:
    ########################################################################
    # Compute coeffs of f05 and store them in Array f05A 
    for n from 1 to T do
      if modp(n,1000)=0 then lprint(n); fi:
      k1:=trunc ((1 + sqrt(1+24*n))/6):
      k2:= trunc( (-1 +sqrt(1+24*n))/6):
      
      for k from 1 to k1 do
        ##nk2:=n-2*k*(3*k-1): if nk2>2000 or nk2<1 then print("n=",n,"k=",k);fi:
        f05A[n]:=f05A[n] - (-1)^k*f05A[n-k*(3*k-1)/2]:
      od:
    for k from 1 to k2 do
        f05A[n]:=f05A[n] - (-1)^k*f05A[n-k*(3*k+1)/2]:
      od:
      f05A[n]:=f05A[n]+aA[n]:
    od:
    ########################################################################
    # SAVING COEFFICIENTS IN DIFFERENT FORMATS
#   (1) Saving coefficients as a matrix in a txt file using ExportMatrix.
    f05AMAT:=Matrix(T,2):
    for j from 1 to T do
       f05AMAT[j,1]:=j:
       f05AMAT[j,2]:=f05A[j]:
    od:
    ExportMatrix("f05AMAT.txt",f05AMAT);
# (2) Saving the Array f05A in a dot-m file:
    save f05A,"f05A.m":
# (3) Saving the f05 as the q-series f05Aq in a dot-m-file
    f05Aq:=add(f05A[n]*q^n,n=0..T):
    save f05Aq, "f05func.m":
    RETURN():
    end:
  comp_f05(T);
quit
